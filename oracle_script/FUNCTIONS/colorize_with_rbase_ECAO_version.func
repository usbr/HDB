CREATE OR REPLACE FUNCTION colorize_with_rbase(
	p_sdi IN NUMBER,
	p_interval IN VARCHAR2,
	p_start_time DATE,
	p_value IN NUMBER)
RETURN VARCHAR2
 /* This function returns a color name to apply to a cell containing
       this value.
       These color names are from the known named colors, originating with X11, 
       but also present in web browsers, .NET, and elsewhere.
       
       Colors were chosen to keep more normally editted data in pale colors, 
       while making failed validations and computation process outputs stand out
       
       LightGray - missing
       Red - failed validation
       SkyBlue - overwrite
       Yellow - computation processor algorithm
       MistyRose - derivation flags is not null
       PaleGreen - none of the above
       
       Example colors at http://en.wikipedia.org/wiki/Web_colors
       
       Initially written by Andrew Gilmore, May 20, 2008
       Modified and function name change by M. Bogner 03/20/09
       Modified for different color features and to include Riverware by M. Bogner 07/27/11
       Modified for ECAO by M. Bogner 12/14/11    */
IS
/* Function to help display data for HDB Poet and other tools:
   given a value and some information, return a color name to
   show as a background for a cell containing that value.

  Example query:
  
  select date_time, value, 
  colorize_value(value, overwrite_flag, validation,method_id,
  derivation_flags) as Color
  from r_day, table(dates_between(TO_Date('05/14/2008', 'MM/dd/yyyy'),
                                  TO_Date('05/19/2008', 'MM/dd/yyyy'),
                                  'day')) dates
  where
  start_date_time(+) = dates.date_time and
  site_datatype_id(+) = 1923
  order by dates.date_time;
  
  Output:
  
  DATE_TIME	VALUE	COLOR
14-MAY-08 00:00	7467.78	SkyBlue
15-MAY-08 00:00	null	LightGray
16-MAY-08 00:00	7400.09	MistyRose
17-MAY-08 00:00	7469.35	PaleGreen
18-MAY-08 00:00	7470.09	Yellow
19-MAY-08 00:00	null	LightGray


   */

/* now the local variables needed  */

l_value NUMBER;
l_overwrite VARCHAR2(1);
l_validation VARCHAR2(1);
l_app_id NUMBER;
l_comp_id NUMBER;
l_data_flags VARCHAR2(20);
l_collect_id NUMBER;
l_manual_edit_app VARCHAR2(1);

BEGIN

    SELECT r.value,
           r.overwrite_flag,
           r.validation,
           r.loading_application_id,
           r.computation_id,
           r.data_flags,
           r.collection_system_id,
           hlap.manual_edit_app
    INTO l_value,
         l_overwrite,
         l_validation,
         l_app_id,
         l_comp_id,
         l_data_flags,
         l_collect_id,
         l_manual_edit_app
    FROM r_base r
    JOIN hdb_loading_application hlap ON r.loading_application_id = hlap.loading_application_id
    WHERE r.site_datatype_id = p_sdi
      AND r.interval = p_interval
      AND r.start_date_time = p_start_time;

    IF p_value IS NULL AND l_value IS NULL THEN
        RETURN 'LightGray';
    ELSE
        RETURN CASE
            WHEN l_validation = 'F' THEN 'Red'
            WHEN ABS(p_value - l_value) > 0.01 THEN 'HotPink'
            WHEN p_value IS NOT NULL AND l_value IS NULL THEN 'DarkOrange'
            WHEN l_app_id = 7 AND l_collect_id = 5 AND l_overwrite = 'O' THEN 'DarkTurquoise'
            WHEN l_app_id = 7 AND l_collect_id = 5 THEN 'Aquamarine'
            WHEN l_app_id = 7 AND l_collect_id = 9 AND l_overwrite = 'O' THEN 'DarkKhaki'
            WHEN l_app_id = 7 AND l_collect_id = 9 THEN 'PaleGoldenrod'
            WHEN l_manual_edit_app != 'Y' AND l_comp_id < 100 AND l_overwrite IS NULL THEN 'PaleGreen'
            WHEN l_manual_edit_app != 'Y' AND l_comp_id < 100 AND l_overwrite = 'O' THEN 'DarkSeaGreen'
            WHEN l_app_id IN (46) AND l_overwrite = 'O' THEN 'Purple'
            WHEN l_app_id IN (46) THEN 'BlueViolet'
            WHEN l_manual_edit_app = 'Y' AND l_comp_id < 100 AND l_overwrite = 'O' THEN 'RoyalBlue'
            WHEN l_manual_edit_app = 'Y' AND l_comp_id < 100 AND l_overwrite IS NULL THEN 'SkyBlue'
            WHEN l_app_id IN (41, 42, 47) AND l_overwrite = 'O' THEN 'Goldenrod'
            WHEN l_app_id IN (42, 47) THEN 'Yellow'
            WHEN l_app_id IN (41) THEN 'Gold'
            WHEN l_data_flags IS NOT NULL THEN 'MistyRose'
            ELSE 'PaleGreen'
        END;
    END IF;
EXCEPTION
  WHEN NO_DATA_FOUND THEN
    RETURN 'PaleGreen';
END colorize_with_rbase;
.
/

GRANT EXECUTE ON colorize_with_rbase TO PUBLIC;
CREATE OR REPLACE PUBLIC SYNONYM colorize_with_rbase FOR colorize_with_rbase;

